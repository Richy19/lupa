import FFI from std.system

export SockAddr, Socket, Buffer, TCP, SOCK, AF, PROTO, SocketError, AcceptError

var int32 = FFI.typeof('int32_t')
FFI cdef """
   typedef uint16_t in_port_t;
   typedef uint32_t socklen_t;
   typedef unsigned short int sa_family_t;

   typedef struct SockAddr {
     sa_family_t sa_family;
     char        sa_data[14];
   } SockAddr;

   typedef struct INET_Addr {
     uint32_t       s_addr;
   } INET_Addr;

   typedef struct INET6_Addr {
     unsigned char  s6_addr[16];
   } INET6_Addr;

   typedef struct SockAddr_INET {
     sa_family_t    sin_family;
     in_port_t      sin_port;
     INET_Addr      sin_addr;
     unsigned char  sin_zero[8]; /* padding, should not vary by arch */
   } SockAddr_INET;

   typedef struct SockAddr_INET6 {
     sa_family_t    sin6_family;
     in_port_t      sin6_port;
     uint32_t       sin6_flowinfo;
     INET6_Addr     sin6_addr;
     uint32_t       sin6_scope_id;
   } SockAddr_INET6;

   typedef struct SockAddr_UNIX {
     sa_family_t sun_family;
     char        sun_path[108];
   } SockAddr_UNIX;

   typedef struct Socket {
      int        so_fd;
      SockAddr *so_addr;
   } Socket;

   int socket(int domain, int type, int protocol);
   int socketpair(int domain, int type, int protocol, int sv[2]);
   int bind(int sockfd, SockAddr *addr, socklen_t addrlen);
   int listen(int sockfd, int backlog);
   int connect(int sockfd, SockAddr *addr, socklen_t addrlen);
   int accept(int sockfd, SockAddr *addr, socklen_t *addrlen);
   int accept4(int sockfd, SockAddr *addr, socklen_t *addrlen, int flags);
   int getsockname(int sockfd, SockAddr *addr, socklen_t *addrlen);
   int getpeername(int sockfd, SockAddr *addr, socklen_t *addrlen);
   int shutdown(int sockfd, int how);

   typedef struct HostEnt {
      char    *h_name;        /* official name of host */
      char    **h_aliases;    /* alias list */
      int     h_addrtype;     /* host address type */
      int     h_length;       /* length of address */
      char    **h_addr_list;  /* list of addresses from name server */
   } HostEnt;

   HostEnt * gethostbyname(const char *name);

   int inet_aton(const char *cp, INET_Addr *pin);
   char * inet_ntoa(INET_Addr in);

   ssize_t read(int fildes, void *buf, size_t nbyte);
   ssize_t write(int fildes, const void *buf, size_t nbyte);

  /* FIXME: on BE arch this is a null macro */
   uint16_t htons(uint16_t hostshort);
"""

enum SOCK {
   STREAM    = 1,
   DGRAM     = 2,
   RAW       = 3,
   RDM       = 4,
   SEQPACKET = 5,
   DCCP      = 6,
   PACKET    = 10,

   CLOEXEC   = 02000000,
   NONBLOCK  = 04000,
}

enum AF {
   UNSPEC     = 0,
   LOCAL      = 1,
   UNIX       = 1, // AF_LOCAL
   FILE       = 1, // AF_LOCAL
   INET       = 2,
   AX25       = 3,
   IPX        = 4,
   APPLETALK  = 5,
   NETROM     = 6,
   BRIDGE     = 7,
   ATMPVC     = 8,
   X25        = 9,
   INET6      = 10,
   ROSE       = 11,
   DECnet     = 12,
   NETBEUI    = 13,
   SECURITY   = 14,
   KEY        = 15,
   NETLINK    = 16,
   ROUTE      = 16, // AF_NETLINK
   PACKET     = 17,
   ASH        = 18,
   ECONET     = 19,
   ATMSVC     = 20,
   RDS        = 21,
   SNA        = 22,
   IRDA       = 23,
   PPPOX      = 24,
   WANPIPE    = 25,
   LLC        = 26,
   CAN        = 29,
   TIPC       = 30,
   BLUETOOTH  = 31,
   IUCV       = 32,
   RXRPC      = 33,
   ISDN       = 34,
   PHONET     = 35,
   IEEE802154 = 36,
   CAIF       = 37,
   ALG        = 38,
   MAX        = 39,
}

enum PROTO {
   TCP = 6,
   UDP = 17,
}

class SockAddr with FFI::MetaType['SockAddr'] {
   var ffi = require('ffi')
   var C = FFI::C

   method family { self::sa_family }
   method family=(f : AF) { self::sa_family = f }

   method data { self::sa_data }
   method data=(d) { self::sa_data = d }

   method len {
      switch self.family {
         case AF.INET:
            return FFI.sizeof('SockAddr_INET')
         case AF.UNIX:
            return FFI.sizeof('SockAddr_UNIX')
         case AF.INET6:
            return FFI.sizeof('SockAddr_INET6')
         default:
            throw "unkown address family ${self.family}"
      }
   }

   static method coerce(that) {
      if that does SockAddr::Common {
         var v = ffi::cast('SockAddr*', ffi::cast('void*', that))
         return v
      }
      throw TypeError.new("cannot coerce ${that} to ${self}")
   }

   trait Common {
      method len { FFI.sizeof(self) }
      static method coerce(that : SockAddr) {
         return FFI.cast(self, that)
      }
   }

   class INET with FFI::MetaType['SockAddr_INET'], Common {
      var C = FFI::C

      method family { self::sin_family }
      method family=(f : AF) { self::sin_family = f }

      method init(host, port) {
         self::sin_family = AF.INET
         if port {
            self::sin_port = C::htons(port)
         }
         if host {
            self::sin_addr = SockAddr::INET::Addr.new(host)
         }
      }
      class Addr with FFI::MetaType['INET_Addr'] {
         method init(host) {
            if host is String {
               var stat = Socket.inet_aton(host, self)
               if stat != 1 {
                  Error raise FFI.strerror(FFI.errno)
               }
            }
            else if host is int32 {
               self::s_addr = host
            }
            else {
               TypeError raise "host ${host} not valid"
            }
         }
      }
   }
   class UNIX with FFI::MetaType['SockAddr_UNIX'], Common {
   }
   class INET6 with FFI::MetaType['SockAddr_INET6'], Common {
      class Addr with FFI::MetaType['INET6_Addr'] {

      }
   }
}

class Socket with FFI::MetaType['Socket'] {
   var C = FFI::C

   method init(domain : AF, type : SOCK, proto : PROTO) {
      var fd = C::socket(domain, type, proto)
      if fd == -1 { return nil, FFI.strerror(FFI.errno) }
      self::so_fd   = fd
      self::so_addr = nil
   }

   method bind(addr : SockAddr) {
      var stat = C::bind(self::so_fd, addr, addr.len)
      if stat == -1 { SocketError raise FFI.strerror(FFI.errno) }
      self::so_addr = addr
      return stat
   }
   method connect(addr : SockAddr) {
      var stat = C::connect(self::so_fd, addr, addr.len)
      if stat == -1 { SocketError raise FFI.strerror(FFI.errno) }
      self::so_addr = addr
   }
   method listen(backlog = 16) {
      C::listen(self::so_fd, backlog)
   }

   method accept {
      var cli_addr     = FFI.new('SockAddr')
      cli_addr.family  = self::so_addr.family

      var cli_addr_len = FFI.new('socklen_t[1]', self::so_addr.len)
      var so_fd = C::accept(self::so_fd, cli_addr, cli_addr_len)

      if so_fd == -1 {
         AcceptError raise FFI.strerror(FFI.errno)
      }
      var sock = FFI.new('Socket')

      sock::so_fd   = so_fd
      sock::so_addr = cli_addr

      return sock
   }

   static method inet_aton(cp : String, pin : SockAddr::INET::Addr) {
      C::inet_aton(cp, pin)
   }
   static method inet_ntoa(n) {
      C::inet_ntoa(n)
   }

   class INET from self {
      method init(type : SOCK, proto : PROTO) {
         super.init(AF.INET, type, proto)
      }
   }
}

class SocketError from Error { }
class AcceptError from SocketError { }

object TCP {
   class Client from Socket::INET {
      var C = FFI::C
      method init {
         super.init(SOCK.STREAM, PROTO.TCP)
      }
      method connect(host, port) {
         var addr = FFI.new('SockAddr_INET')
         var inet = FFI.new('INET_Addr')
         if C::inet_aton(host, inet) == 0 {
            var hostent = C::gethostbyname(host)
            var host = FFI.cast('INET_Addr*', hostent::h_addr_list::[0])
            addr = FFI.new('SockAddr_INET')
            addr::sin_family = AF.INET
            addr::sin_addr = host::[0]
            if port {
               addr::sin_port = C::htons(port)
            }
         }
         else {
            addr = SockAddr::INET.new(host, port)
         }
         super.connect(addr)
      }
      method read(buf, len) {
         C::read(self::so_fd, buf, len)
      }
      method write(buf, len) {
         C::write(self::so_fd, buf, len)
      }
   }
   class Server from Socket::INET {
      method init {
         super.init(SOCK.STREAM, PROTO.TCP)
      }
      method bind(host, port) {
         var addr = SockAddr::INET.new(host, port)
         super.bind(addr)
      }
   }
}

class AddrInfo {
   FFI cdef """
      typedef struct AddrInfo {
         int ai_flags;           /* input flags */
         int ai_family;          /* protocol family for socket */
         int ai_socktype;        /* socket type */
         int ai_protocol;        /* protocol for socket */
         socklen_t ai_addrlen;   /* length of socket-address */
         SockAddr *ai_addr;    /* socket-address for socket */
         char *ai_canonname;     /* canonical name for service location */
         struct AddrInfo *ai_next; /* pointer to next in list */
      } AddrInfo;

      int getaddrinfo(const char *host, const char *port, AddrInfo *hints, AddrInfo **res);
      void freeaddrinfo(AddrInfo *ai);
   """

   with FFI::MetaType['AddrInfo']

   method init(host : String, port : Number, hint : AddrInfo) {
      var info = FFI.new('AddrInfo[1]')
      var retv = FFI::C::getaddrinfo(host, FFI::C::htons(port), hint, info)
      if retv == -1 {
         Error raise FFI.strerror(FFI.errno)
      }
   }
}

class Buffer {
   FFI cdef """
      typedef struct Buffer {
         size_t  size;
         size_t  offs;
         uint8_t *data;
      } Buffer;
   """

   with FFI::MetaType['Buffer']

   method init(size : Number = 512) {
      self::size = size
      self::data = FFI::C::malloc(size)
      self::offs = 0
   }
   method need(size) {
      if self::size < self::offs + size {
         var new_size = self::offs + size
         while new_size < self::size {
            new_size = new_size * 2
         }
         self::data = FFI::C::realloc(FFI.cast('void*', self::data), new_size)
         self::size = new_size
      }
   }
   self::__gc = function(self) {
      FFI::C::free(self::data)
   }
}


