export Template

object Markup {
   rule match {
      {@ (<const> (<code> <const>)*) @} -> function(a) { a.concat() }
   }
   rule code {
      <intrp> | <logic>
   }
   rule intrp {
      "<%=" %s* {~ { (!(%s* "%>") .)+ } -> function(s) {
         return "__out.push(%s);".format(s)
      } ~} %s* "%>"
   }
   rule logic {
      "<%" %s* { (!"%>" .)+ } "%>" %s*
   }
   rule const {
      {~ { (!<code> .)* } -> function(s) {
         if s != "" {
            return "__out.push(%q);".format(s)
         }
      } ~}
   }
}

object Compiler {

    var CACHE = { }

    method compile(source) {
        if CACHE[source] {
            return CACHE[source]
        }
        var code = Markup.match(source)
        var luac = Function(code, '__out', '__env')
        CACHE[source] = luac
        return luac
    }

    method clear_cache() {
        CACHE = { }
    }
}

import open from io

class Template {
    has _base = "."

    var Environment = { __index = _G }

    method __init(base) {
        self._base = base
    }
    method process_file(path, vars) {
        var path = self._base ~ '/' ~ path
        var file = assert(open(path, "r"))
        var data = file.read("*a")
        file.close()
        return self.process(data, vars, path)
    }
    method process(src, vars, name) {
        var code = Compiler.compile(src)
        var out = [ ] 
        var env = { } as Environment

        if vars {
           for k,v in vars { env[k] = v }
        }

        code(out, env)

        return out.concat()
    }
}

/*
var s = """
<html>
<body>
  <ul>
<% var a = ['a','b','c','d','e'] %>
<% for i, v in a { %>
    <li> <%= i %> => <%= v %> </li>
<% } %>
  </ul>
</body>
</html>
"""
var t = Template()
print(t.process(s))
for i=1, 100000 {
    t.process(s)
}
*/


